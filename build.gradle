plugins {
	id 'java-library'
    id 'jacoco'
    id "org.sonarqube" version "6.2.0.5505"
    id 'signing'
	id 'maven-publish'
}

group = 'io.github.qishr'
version = '0.1.0'

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	testImplementation(platform('org.junit:junit-bom:5.13.3'))
    testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
    testImplementation 'org.mockito:mockito-core:5.18.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.18.0'
	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

java {
    // withJavadocJar()
    // withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        html.required = true
    }
}

jacoco {
    toolVersion = "0.8.13"
}

sonar {
  properties {
    property "sonar.projectKey", "qishr_cascara"
    property "sonar.organization", "qishr"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

def user = ["whoami"].execute().text.trim()
jar {
    archiveBaseName.set(rootProject.name)
    manifest {
        attributes( 'Implementation-Title': 'cascara-common',
                    'Implementation-Version': version,
                    'Implementation-Vendor': group,
                    'Automatic-Module-Name': 'cascara.common',
                    'Built-By': "${user}",
                    'Built-Date': new Date())
    }
}

jar.doLast {
    def jarFile = archiveFile.get().asFile
    ant.checksum file: jarFile
}

signing {
    sign publishing.publications
}

// configure the maven publication
publishing {
    publications {
        myPub(MavenPublication) {
            from components.java
            pom {
                name = 'cascara-common'
                description = 'Cascara Common'
                url = 'https://qishr.github.io/cascara-common'
                licenses {
                    license {
                        name = 'GPLv3Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id = 'qishr'
                        name = 'Sandy Dunlop'
                        email = 'sandy.dunlop@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/qishr/cascara-common.git'
                    developerConnection = 'scm:git:ssh://github.com:qishr/cascara-common.git'
                    url = 'https://github.com/qishr/cascara-common'
                }
            }
        }
    }

	repositories {
        maven {
            name = "LocalMavenWithChecksums"
            url = uri(layout.buildDirectory.dir("staging-deploy"))
        }
    }
}
